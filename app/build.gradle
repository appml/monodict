// https://github.com/excilys/androidannotations/wiki/Building-Project-Gradle
apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'com.droidtitan.lintcleaner'

// http://forums.gradle.org/gradle/topics/how_can_i_store_the_git_revision_for_use_in_the_project
def getVersionSuffixForDebug() {
    def cmd = "git rev-parse --short HEAD"
    def proc = cmd.execute()
    return "-debug-" + proc.text.trim()
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName 'com.github.wakhub.monodict'
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "com.github.wakhub.monodict"
        minSdkVersion 19
        targetSdkVersion 25
        //          | | | |
        versionCode 3030000
        versionName '3.3'
//        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            jniLibs.srcDir 'src/main/libs'
            jni.srcDirs = [] // disable automatic ndk-build
        }
    }

    signingConfigs {
        release {
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix getVersionSuffixForDebug()
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    // http://qiita.com/wada811/items/ab48fe40bbdb895f9f40
    applicationVariants.all { variant ->
        def output = variant.outputs.get(0)
        File apk = output.outputFile
        String newName = output.outputFile.name
                .replace(".apk", "-${variant.mergedFlavor.versionCode}-${variant.mergedFlavor.versionName}.apk")
                .replace("app-", "${variant.mergedFlavor.applicationId}-")
        output.outputFile = new File(apk.parentFile, newName)
    }

    useLibrary 'org.apache.http.legacy'
}

File signingPropertiesFile = rootProject.file('credentials/release.keystore.properties')
if (signingPropertiesFile.exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(signingPropertiesFile))
    android.signingConfigs.release.storeFile = file(props.storeFile)
    android.signingConfigs.release.storePassword = props.storePassword
    android.signingConfigs.release.keyAlias = props.keyAlias
    android.signingConfigs.release.keyPassword = props.keyPassword
}

// https://github.com/marcoRS/lint-cleaner-plugin
lintCleaner {
    ignoreResFiles = true
}

dependencies {
    


    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':android-support-v4-preferencefragment')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:gridlayout-v7:25.3.1'
    compile 'com.android.support:support-annotations:21.0.3'
    compile 'org.androidannotations:androidannotations-api:3.2'
    compile 'com.afollestad.material-dialogs:core:0.9.4.4'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'com.google.code.gson:gson:2.3@jar'
    compile 'com.google.guava:guava:18.0'
    compile 'com.j256.ormlite:ormlite-core:4.48@jar'
    compile 'com.j256.ormlite:ormlite-android:4.48@jar'
    compile 'com.melnykov:floatingactionbutton:1.1.0'
    compile 'com.mobsandgeeks:android-saripaar:1.0.3'
    compile 'com.nispok:snackbar:2.7.0'
    compile 'com.readystatesoftware.sqliteasset:sqliteassethelper:2.0.1'
    compile 'com.google.firebase:firebase-storage:10.0.1'
    compile 'com.google.firebase:firebase-auth:10.0.1'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.2.1'
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'
    apt 'org.androidannotations:androidannotations:3.2'
}


apply plugin: 'com.google.gms.google-services'
